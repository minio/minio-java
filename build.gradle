/*
 * Minio Java Library for Amazon S3 Compatible Cloud Storage, (C) 2015,2016 Minio, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*************************/
/* gradleVersion = '3.1' */
/*************************/

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'

    group = 'io.minio'
    version = '2.0.5'
    if (!project.hasProperty('release')) {
        version += '-DEV'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "com.google.http-client:google-http-client-xml:1.20.0"
        compile "com.google.guava:guava:18.0"
        compile "com.squareup.okhttp:okhttp:2.7.2"
        compile "com.squareup.okhttp:mockwebserver:2.7.2"
        compile "com.squareup.okio:okio:1.6.0"
        compile "joda-time:joda-time:2.7"
	compile "com.fasterxml.jackson.core:jackson-annotations:2.8.4"
	compile "com.fasterxml.jackson.core:jackson-core:2.8.4"
	compile "com.fasterxml.jackson.core:jackson-databind:2.8.4"

        testCompile "junit:junit:4.12"
    }

    [compileJava, compileTestJava].each() {
        it.options.fork = true
        it.options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:-options"]
        it.options.encoding = "UTF-8"
    }

    checkstyle {
        toolVersion = "6.5"
        configFile = file("$rootDir/checkstyle.xml")
        ignoreFailures = false
        if (rootProject.hasProperty("checkstyle.ignoreFailures")) {
            ignoreFailures = rootProject.properties["checkstyle.ignoreFailures"].toBoolean()
        }
    }

    checkstyleMain {
        source = fileTree(dir: "$rootDir/api/src/main", include: "**/*.java")
	source += fileTree(dir: "$rootDir/functional", include: "**/*.java")
	source += fileTree(dir: "$rootDir/examples", include: "**/*.java")
    }

    checkstyleTest {
        source = fileTree(dir: "$rootDir/api/src/test", include: "**/*.java")
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
         }
    }

    test {
        // Show stacktrace on test failure than opening in web browser.
        testLogging {
            exceptionFormat = 'full'
            showExceptions true
            showCauses true
            showStackTraces true
        }
    }

    task localeTest(type: Test) {
        description = "Runs tests with locale de.DE"
        System.setProperty('user.language', 'de')
        System.setProperty('user.country', 'DE')
        systemProperties = System.properties
        dependsOn test
    }

    check.dependsOn localeTest

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

project(':api') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven'
    apply plugin: 'signing'

    archivesBaseName = 'minio-java'

    jar {
        manifest {
            attributes('Implementation-Title': archivesBaseName,
            'Implementation-Version': version,
            'Built-By': System.getProperty('user.name'),
            'Built-JDK': System.getProperty('java.version'),
            'Source-Compatibility': sourceCompatibility,
            'Target-Compatibility': targetCompatibility)
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc.options {
        encoding = 'UTF-8'
        links 'https://docs.oracle.com/javase/8/docs/api/'
    }

    // Disable JavaDoc doclint on Java 8. It's annoying.
    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }

    artifacts {
        archives javadocJar, sourcesJar, shadowJar
    }

    signing {
        if (project.properties.containsKey('signing.keyId')) {
            sign configurations.archives
        }
    }

    ext.deployUsername = ''
    if (project.properties.containsKey('ossrhUsername')) {
        ext.deployUsername = ext.ossrhUsername
    }

    ext.deployPassword = ''
    if (project.properties.containsKey('ossrhPassword')) {
        ext.deployPassword = ext.ossrhPassword
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: deployUsername, password: deployPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: deployUsername, password: deployPassword)
                }

                pom.project {
                    name 'minio'
                    packaging 'jar'
                    description 'Minio Java Library for Amazon S3 Compatible Cloud Storage'
                    url 'https://github.com/minio/minio-java'

                    scm {
                        connection 'scm:git:git@github.com:minio/minio-java.git'
                        developerConnection 'scm:git:git@github.com:minio/minio-java.git'
                        url 'http://github.com/minio/minio-java'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'minio'
                            name 'Minio Inc.'
                            email 'dev@minio.io'
                        }
                    }
                }
            }
        }
    }

}

project(':examples') {
    dependencies {
        compile project(':api')
    }

    sourceSets {
        main {
	    java {
	        srcDirs = ["$rootDir/examples"]
	    }
	}
    }
}

project(':functional') {
    dependencies {
        compile project(':api')
    }

    sourceSets {
        main {
            java {
                srcDirs = ["$rootDir/functional"]
            }
        }
    }

    task runFunctionalTest(type:JavaExec) {
        main = 'FunctionalTest'
        classpath = sourceSets.main.runtimeClasspath
        args = ["https://play.minio.io:9000", "Q3AM3UQ867SPQQA43P2F", "zuf+tfteSlswRu7BJ86wekitnifILbZam1KYY3TG"]
    }

}
